plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion fenrirCompileSDK
    buildToolsVersion fenrirBuildTools
    ndkVersion fenrirNdk

    defaultConfig {
        minSdkVersion fenrirMinSDK
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion fenrirTargetSDK
        versionCode FenrirVersionCode
        versionName FenrirVersionName

        buildConfigField 'String', 'SERVICE_TOKEN', getProps("vk_service_token")
        buildConfigField 'String', 'FAKE_ABI', getProps("fake_abi")
        buildConfigField 'String', 'FAKE_DEVICE', getProps("fake_device")
        buildConfigField 'String', 'FCM_SESSION_ID_GEN_URL', getProps("fcm_session_id_gen_url")
        buildConfigField 'String', 'KATE_RECEIPT_GMS_TOKEN', getProps("kate_receipt_gms_token")
        buildConfigField 'boolean', 'MANAGE_SCOPED_STORAGE', fenrirTargetSDK >= 30 ? 'true' : 'false'
        resValue "string", "kate_receipt_gms_token", getProps("kate_receipt_gms_token")

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            //options.compilerArgs << "-Xlint:deprecation"
            options.compilerArgs << "-Xmaxwarns" << "1000" << "-Xmaxerrs" << "1000"
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled fenrirMinSDK < 26

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        encoding 'utf-8'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }

    flavorDimensions "type"

    productFlavors {
        fenrir_vk_lite {
            applicationId "dev.ragnarok.fenrir_public"
            dimension "type"
            buildConfigField 'boolean', 'IS_FULL', 'false'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.VK_ANDROID'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        }
        fenrir_vk_full {
            applicationId "dev.ragnarok.fenrir_full"
            dimension "type"
            buildConfigField 'boolean', 'IS_FULL', 'true'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.VK_ANDROID'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
            isDefault = true
        }
        fenrir_kate_full {
            applicationId "dev.ragnarok.fenrir_kate_full"
            dimension "type"
            buildConfigField 'boolean', 'IS_FULL', 'true'
            buildConfigField 'int', 'VK_API_APP_ID', getProps("kate_app_id")
            buildConfigField 'int', 'DEFAULT_ACCOUNT_TYPE', 'Account_Types.KATE'
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("kate_client_secret")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('app.build_config')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if (fis != null) fis.close()
        }
    } else {
        throw new GradleException('File app.build_config not found')
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-parcelize-runtime:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-android-extensions-runtime:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    compileOnly "org.checkerframework:checker-qual:$checkerQual"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0-alpha02'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.customview:customview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.3'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation "io.reactivex.rxjava3:rxjava:$RxJavaVersion"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.google.firebase:firebase-database:20.0.1'
    fenrir_vk_liteImplementation('com.google.firebase:firebase-messaging:22.0.0') {
        exclude group: "com.google.firebase", module: "firebase-installations"
    }
    fenrir_vk_liteImplementation project(path: ':firebase-installations')
    fenrir_vk_fullImplementation('com.google.firebase:firebase-messaging:22.0.0') {
        exclude group: "com.google.firebase", module: "firebase-installations"
    }
    fenrir_vk_fullImplementation project(path: ':firebase-installations')
    //noinspection GradleDependency
    fenrir_kate_fullImplementation 'com.google.firebase:firebase-messaging:20.1.0'
    fenrir_vk_fullImplementation 'com.google.firebase:firebase-datatransport:18.0.1'
    fenrir_vk_liteImplementation 'com.google.firebase:firebase-datatransport:18.0.1'
    //implementation project(path: ':libfenrir')
    implementation project(path: ':picasso')
    implementation project(path: ':ucrop')
    implementation project(path: ':image')
    implementation project(path: ':material')
    implementation project(path: ':viewpager2')
    implementation project(path: ':zxing-android-embedded')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation "com.squareup.okio:okio:$okioVersion"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoLibraryVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoLibraryVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha03'
    implementation 'androidx.media:media:1.4.1'
    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.6'
    implementation 'androidx.work:work-runtime-ktx:2.6.0-rc01'
    implementation "com.google.guava:guava:$GuavaVersion"
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    implementation 'androidx.loader:loader:1.1.0'
    implementation 'com.caverock:androidsvg:1.4'
    implementation 'androidx.collection:collection-ktx:1.1.0'
}

apply plugin: 'com.google.gms.google-services'
